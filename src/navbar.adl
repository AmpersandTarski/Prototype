CONTEXT PrototypeFramework IN ENGLISH

    --[INTERFACES]
    CONCEPT PF_Interface ""
    REPRESENT PF_Interface TYPE OBJECT

    RELATION ifcRoles[PF_Interface*PF_Role]
    MEANING "This relation contains the roles for which an interface is available (empty means: available for all roles)."
        CONCEPT PF_Role ""
        REPRESENT PF_Role TYPE OBJECT
    
    RELATION isPublic[PF_Interface*PF_Interface] [PROP]
    MEANING "This properties defines if an interface is accessible for everyone (i.e. public)"

    RULE "Is public equality" : isPublic = I-(ifcRoles;ifcRoles~)
    MEANING "An interface is considered public when it not occurs in ifcRoles"

        ROLE ExecEngine MAINTAINS "Add interface isPublic property"
        RULE "Add interface isPublic property": I-(ifcRoles;ifcRoles~) |- isPublic
        VIOLATION (TXT "{EX}InsPair;isPublic;PF_Interface;", SRC I, TXT ";PF_Interface;", SRC I)

        ROLE ExecEngine MAINTAINS "Remove interface isPublic property"
        RULE "Remove interface isPublic property": I /\ ifcRoles;ifcRoles~ |- I-isPublic
        VIOLATION (TXT "{EX}DelPair;isPublic;PF_Interface;", SRC I, TXT ";PF_Interface;", SRC I)

    --[NAVBAR ITEMS]-------------------------------------------------------------------------------
    CONCEPT PF_NavMenuItem ""
    REPRESENT PF_NavMenuItem TYPE OBJECT
  
    RELATION label[PF_NavMenuItem*PF_Label] [UNI,TOT] 
    MEANING "The human readable text that represents the navbar item and is shown to the user"
        CONCEPT PF_Label ""
        REPRESENT PF_Label TYPE ALPHANUMERIC
    
    RELATION ifc[PF_NavMenuItem*PF_Interface] [UNI] 
        MEANING "A navbar item can direct to an interface"
        CONCEPT PF_Interface "Representation of an Ampersand interface"
  
    RELATION url[PF_NavMenuItem*PF_URL] [UNI]
    MEANING "A navbar item can direct to an (external) url. This can also be used for extensions like the excelimporter"
        CONCEPT PF_URL "a uniform resource locator as defined by RFC3986"
        REPRESENT PF_URL TYPE BIGALPHANUMERIC
  
    RELATION seqNr[PF_NavMenuItem*PF_SeqNr] [UNI]
    MEANING "A navbar item has a seq nr (default 0) which determines the position of the item"
        CONCEPT PF_SeqNr ""
        REPRESENT PF_SeqNr TYPE INTEGER
    
    RELATION subItemOf[PF_NavMenuItem*PF_NavMenuItem] [UNI,IRF,ASY]
    MEANING "A navbar item can be a sub item of another item"

    -- Not yet implemented (i.e. has no effect)
    RELATION sessionMenuItem[SESSION*PF_NavMenuItem]
    MEANING "The navbar items that are part of the top level navigation bar"
    
    --[NAVBAR MENUS]-------------------------------------------------------------------------------
    CONCEPT PF_NavMenu ""
    CLASSIFY PF_NavMenu ISA PF_NavMenuItem
    REPRESENT PF_NavMenu TYPE OBJECT

    RELATION isPartOf[PF_NavMenuItem*PF_NavMenu] [UNI,TOT]
    MEANING "A navbar item is part of a certain navmenu"

    ROLE ExecEngine MAINTAINS "Fix TOT for isPartOf[PF_NavMenuItem*PF_NavMenu]"
    RULE "Fix TOT for isPartOf[PF_NavMenuItem*PF_NavMenu]": I[PF_NavMenuItem];subItemOf;isPartOf |- isPartOf
    VIOLATION (TXT "{EX}InsPair;isPartOf;PF_NavMenuItem;", SRC I, TXT ";PF_NavMenu;", TGT I)

    --[ADMIN INTERFACES to adapt the menus]--------------------------------------------------------
    INTERFACE "Edit navigation menu" FOR Admin: "_SESSION";V[SESSION*PF_NavMenu] INTERFACE "Edit menu item"
    
    INTERFACE "Edit menu item" FOR Admin: I[PF_NavMenuItem] cRud BOX
        [ "Label"        : label
        , "Items"        : subItemOf~ CRuD BOX <SCOLS>
            [ "#"        : seqNr                    cRUd
            , "Label"    : label                    cRUd
            , "Ifc"      : ifc                      cRUd
            , "Url"      : url                      cRUd
            , "Edit"     : I LINKTO INTERFACE "Edit menu item"
            ]
        ]

    --[SYSTEM INTERFACES to adapt the menus]-------------------------------------------------------
    API "PF_MenuItems" FOR SYSTEM: I[PF_NavMenu];isPartOf~ BOX
        [ "label"           : label
        , "seqNr"           : seqNr
        , "ifc"             : ifc
        , "url"             : url
        , "parent"          : subItemOf
        ]
    
ENDCONTEXT