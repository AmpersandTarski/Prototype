CONTEXT PrototypeFramework IN ENGLISH

    -- TEMPORARY. TODO: replace Role by PF_Role in other meta model scripts
    CLASSIFY Role ISA PF_Role

    --[INTERFACES]
    CONCEPT PF_Interface ""
    REPRESENT PF_Interface TYPE OBJECT

    RELATION ifcRoles[PF_Interface*PF_Role]
    MEANING "This relation contains the roles for which an interface is available"
    -- TODO: empty used to be that the interface is public, this is now implemented with an isPublic property
        CONCEPT PF_Role ""
        REPRESENT PF_Role TYPE OBJECT
    
    RELATION isPublic[PF_Interface*PF_Interface] [PROP]
    MEANING "This properties defines if an interface is accessible for everyone (i.e. public)"

        RULE "Public interface integrity" : V[PF_Role*PF_Interface];isPublic |- ifcRoles~

        ROLE ExecEngine MAINTAINS "Add public interfaces to all roles"
        RULE "Add public interfaces to all roles": V[PF_Role*PF_Interface];isPublic |- ifcRoles~
        VIOLATION (TXT "{EX}InsPair;ifcRoles;PF_Interface;", TGT I, TXT ";PF_Role;", SRC I)

    INTERFACE "Edit interface" FOR Admin : I[PF_Interface] BOX
        [ "Interface"           : I
        , "Is public"           : isPublic      cRUd
        , "For roles"           : ifcRoles      cRUd
        ]

    --[NAVBAR ITEMS]-------------------------------------------------------------------------------
    CONCEPT PF_NavMenuItem ""
    REPRESENT PF_NavMenuItem TYPE OBJECT
  
    RELATION label[PF_NavMenuItem*PF_Label] [UNI,TOT] 
    MEANING "The human readable text that represents the navbar item and is shown to the user"
        CONCEPT PF_Label ""
        REPRESENT PF_Label TYPE ALPHANUMERIC
    
    RELATION ifc[PF_NavMenuItem*PF_Interface] [UNI] 
        MEANING "A navbar item can direct to an interface"
        CONCEPT PF_Interface "Representation of an Ampersand interface"
  
    RELATION url[PF_NavMenuItem*PF_URL] [UNI]
    MEANING "A navbar item can direct to an (external) url. This can also be used for extensions like the excelimporter"
        CONCEPT PF_URL "a uniform resource locator as defined by RFC3986"
        REPRESENT PF_URL TYPE BIGALPHANUMERIC
  
    RELATION seqNr[PF_NavMenuItem*PF_SeqNr] [UNI]
    MEANING "A navbar item has a seq nr (default 0) which determines the position of the item"
        CONCEPT PF_SeqNr ""
        REPRESENT PF_SeqNr TYPE INTEGER
    
    RELATION isSubItemOf[PF_NavMenuItem*PF_NavMenuItem] [UNI,IRF,ASY]
    MEANING "A navbar item can be a sub item of another item"

    --[SESSION MENU ITEMS (i.e. items that are active/visible for a specific session)]-------------
    RELATION sessionMenuItem[SESSION*PF_NavMenuItem]
    MEANING "The navbar items that are active/visible for a session"

        -- Activate/deactive menu items based on the sessionActiveRoles
        ROLE ExecEngine MAINTAINS "Activate interface menu items"
        RULE "Activate interface menu items": sessionActiveRoles[SESSION*Role];ifcRoles~;ifc~ |- sessionMenuItem
        VIOLATION (TXT "{EX}InsPair;sessionMenuItem;SESSION;", SRC I, TXT ";PF_NavMenuItem;", TGT I)

        ROLE ExecEngine MAINTAINS "Deactivate interface menu items"
        RULE "Deactivate interface menu items": sessionMenuItem;ifc |- sessionActiveRoles[SESSION*Role];ifcRoles~
        VIOLATION (TXT "{EX}DelPair;sessionMenuItem;SESSION;", SRC I, TXT ";PF_NavMenuItem;", TGT I)

        -- Activate/deactive parent menu items if needed
        ROLE ExecEngine MAINTAINS "Activate parent menu items"
        RULE "Activate parent menu items": sessionMenuItem;isSubItemOf |- sessionMenuItem
        VIOLATION (TXT "{EX}InsPair;sessionMenuItem;SESSION;", SRC I, TXT ";PF_NavMenuItem;", TGT I)

        ROLE ExecEngine MAINTAINS "Deactivate empty menu items"
        RULE "Deactivate empty menu items": sessionMenuItem;(I /\ isSubItemOf~;isSubItemOf) |- sessionMenuItem;isSubItemOf
        MEANING "A session menu item (i.e. active item) that has subelements MUST at least have a sub item that is also active"
        VIOLATION (TXT "{EX}DelPair;sessionMenuItem;SESSION;", SRC I, TXT ";PF_NavMenuItem;", TGT I)

    
    --[NAVBAR MENUS]-------------------------------------------------------------------------------
    CONCEPT PF_NavMenu ""
    CLASSIFY PF_NavMenu ISA PF_NavMenuItem
    REPRESENT PF_NavMenu TYPE OBJECT

    RELATION isPartOf[PF_NavMenuItem*PF_NavMenu] [UNI,TOT]
    MEANING "A navbar item is part of a certain navmenu"

    ROLE ExecEngine MAINTAINS "Fix TOT for isPartOf[PF_NavMenuItem*PF_NavMenu]"
    RULE "Fix TOT for isPartOf[PF_NavMenuItem*PF_NavMenu]": I[PF_NavMenuItem];isSubItemOf;isPartOf |- isPartOf
    VIOLATION (TXT "{EX}InsPair;isPartOf;PF_NavMenuItem;", SRC I, TXT ";PF_NavMenu;", TGT I)

    --[ADMIN INTERFACES to adapt the menus]--------------------------------------------------------
    INTERFACE "Edit navigation menu" FOR Admin: "_SESSION";V[SESSION*PF_NavMenu] INTERFACE "Edit menu item"
    
    INTERFACE "Edit menu item" FOR Admin: I[PF_NavMenuItem] cRud BOX
        [ "Label"        : label
        , "Items"        : isSubItemOf~ CRuD BOX <SCOLS>
            [ "# "       : seqNr                    cRUd
            , "Label"    : label                    cRUd
            , "Ifc"      : ifc                      cRUd
            , "Roles"    : ifc;ifcRoles             cRud
            , "Edit"     : I LINKTO INTERFACE "Edit menu item"
            ]
        , "Url"          : url                      cRUd
        ]

    --[SYSTEM INTERFACES to adapt the menus]-------------------------------------------------------
    API "PF_MenuItems" FOR SYSTEM: "_SESSION";sessionMenuItem BOX
        [ "id"              : I
        , "label"           : label
        , "seqNr"           : seqNr
        , "ifc"             : ifc
        , "url"             : url
        , "parent"          : isSubItemOf
        ]
    
ENDCONTEXT